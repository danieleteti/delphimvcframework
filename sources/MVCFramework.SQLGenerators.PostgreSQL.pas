// *************************************************************************** }
//
// Delphi MVC Framework
//
// Copyright (c) 2010-2019 Daniele Teti and the DMVCFramework Team
//
// https://github.com/danieleteti/delphimvcframework
//
// ***************************************************************************
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//
// ***************************************************************************

unit MVCFramework.SQLGenerators.PostgreSQL;

interface

uses
  System.Rtti,
  System.Generics.Collections,
  FireDAC.Phys.FB,
  FireDAC.Phys.FBDef,
  MVCFramework.ActiveRecord,
  MVCFramework.Commons,
  MVCFramework.RQL.Parser;

type
  TMVCSQLGeneratorPostgreSQL = class(TMVCSQLGenerator)
  protected
    function GetCompilerClass: TRQLCompilerClass; override;
  public
    function CreateSelectSQL(
      const TableName: string;
      const Map: TDictionary<TRttiField, string>;
      const PKFieldName: string;
      const PKOptions: TMVCActiveRecordFieldOptions): string; override;
    function CreateInsertSQL(
      const TableName: string;
      const Map: TDictionary<TRttiField, string>;
      const PKFieldName: string;
      const PKOptions: TMVCActiveRecordFieldOptions): string; override;
    function CreateUpdateSQL(
      const TableName: string;
      const Map: TDictionary<TRttiField, string>;
      const PKFieldName: string;
      const PKOptions: TMVCActiveRecordFieldOptions): string; override;
    function CreateDeleteSQL(
      const TableName: string;
      const Map: TDictionary<TRttiField, string>;
      const PKFieldName: string;
      const PKOptions: TMVCActiveRecordFieldOptions;
      const PrimaryKeyValue: Int64): string; override;
    function CreateSelectByPKSQL(
      const TableName: string;
      const Map: TDictionary<TRttiField, string>; const PKFieldName: string;
      const PKOptions: TMVCActiveRecordFieldOptions;
      const PrimaryKeyValue: Int64): string; override;
    function CreateSelectSQLByRQL(
      const RQL: string;
      const Mapping: TMVCFieldsMapping): string; override;
    function CreateSelectCount(
      const TableName: String): String; override;
  end;

implementation

{
  All identifiers (including column names) that are not double-quoted are folded to
  lower case in PostgreSQL. Column names that were created with double-quotes and thereby
  retained upper-case letters (and/or other syntax violations) have to be double-quoted
  for the rest of their life.
}

uses
  System.SysUtils,
  MVCFramework.RQL.AST2PostgreSQL;

function TMVCSQLGeneratorPostgreSQL.CreateInsertSQL(const TableName: string; const Map: TDictionary<TRttiField, string>;
  const PKFieldName: string; const PKOptions: TMVCActiveRecordFieldOptions): string;
var
  lKeyValue: TPair<TRttiField, string>;
  lSB: TStringBuilder;
begin
  lSB := TStringBuilder.Create;
  try
    lSB.Append('INSERT INTO ' + TableName.QuotedString('"') + ' (');
    for lKeyValue in Map do
      lSB.Append(lKeyValue.value + ',');
    lSB.Remove(lSB.Length - 1, 1);
    lSB.Append(') values (');
    for lKeyValue in Map do
    begin
      lSB.Append(':' + lKeyValue.value + ',');
    end;
    lSB.Remove(lSB.Length - 1, 1);
    lSB.Append(')');

    if TMVCActiveRecordFieldOption.foAutoGenerated in PKOptions then
    begin
      lSB.Append(' RETURNING ' + PKFieldName);
      // case GetBackEnd of
      // cbPostgreSQL:
      // begin
      // lSB.Append(' RETURNING ' + fPrimaryKeyFieldName);
      // end;
      // cbMySQL:
      // begin
      // lSB.Append(';SELECT LAST_INSERT_ID() as ' + fPrimaryKeyFieldName);
      // end;
      // else
      // raise EMVCActiveRecord.Create('Unsupported backend engine');
      // end;
    end;
    Result := lSB.ToString;
  finally
    lSB.Free;
  end;
end;

function TMVCSQLGeneratorPostgreSQL.CreateSelectByPKSQL(
  const TableName: string;
  const Map: TDictionary<TRttiField, string>; const PKFieldName: string;
  const PKOptions: TMVCActiveRecordFieldOptions;
  const PrimaryKeyValue: Int64): string;
begin
  Result := CreateSelectSQL(TableName, Map, PKFieldName, PKOptions) + ' WHERE ' +
    PKFieldName + '= :' + PKFieldName; // IntToStr(PrimaryKeyValue);
end;

function TMVCSQLGeneratorPostgreSQL.CreateSelectCount(
  const TableName: String): String;
begin
  Result := 'SELECT count(*) FROM ' + TableName.QuotedString('"');
end;

function TMVCSQLGeneratorPostgreSQL.CreateSelectSQL(const TableName: string;
  const Map: TDictionary<TRttiField, string>; const PKFieldName: string;
  const PKOptions: TMVCActiveRecordFieldOptions): string;
begin
  Result := 'SELECT ' + TableFieldsDelimited(Map, PKFieldName, ',') + ' FROM ' + TableName.QuotedString('"');
end;

function TMVCSQLGeneratorPostgreSQL.CreateSelectSQLByRQL(const RQL: string;
  const Mapping: TMVCFieldsMapping): string;
var
  lPostgreSQLCompiler: TRQLPostgreSQLCompiler;
begin
  lPostgreSQLCompiler := TRQLPostgreSQLCompiler.Create(Mapping);
  try
    GetRQLParser.Execute(RQL, Result, lPostgreSQLCompiler);
  finally
    lPostgreSQLCompiler.Free;
  end;
end;

function TMVCSQLGeneratorPostgreSQL.CreateUpdateSQL(const TableName: string; const Map: TDictionary<TRttiField, string>;
  const PKFieldName: string; const PKOptions: TMVCActiveRecordFieldOptions): string;
var
  keyvalue: TPair<TRttiField, string>;
begin
  Result := 'UPDATE ' + TableName.QuotedString('"') + ' SET ';
  for keyvalue in Map do
  begin
    Result := Result + keyvalue.value + ' = :' + keyvalue.value + ',';
  end;
  Result[Length(Result)] := ' ';
  if not PKFieldName.IsEmpty then
  begin
    Result := Result + ' where ' + PKFieldName + '= :' + PKFieldName;
  end;
end;

function TMVCSQLGeneratorPostgreSQL.GetCompilerClass: TRQLCompilerClass;
begin
  Result := TRQLPostgreSQLCompiler;
end;

function TMVCSQLGeneratorPostgreSQL.CreateDeleteSQL(const TableName: string; const Map: TDictionary<TRttiField, string>;
  const PKFieldName: string; const PKOptions: TMVCActiveRecordFieldOptions; const PrimaryKeyValue: Int64): string;
begin
  Result := 'DELETE FROM ' + TableName.QuotedString('"') + ' WHERE ' + PKFieldName + '= ' + IntToStr(PrimaryKeyValue);
end;

initialization

TMVCSQLGeneratorRegistry.Instance.RegisterSQLGenerator('postgresql', TMVCSQLGeneratorPostgreSQL);

finalization

TMVCSQLGeneratorRegistry.Instance.UnRegisterSQLGenerator('postgresql');

end.
